{1,
{3,
{"Code Template ",1,0,"",""},
{0,
{"Комментарий",0,0,"комм","// НаименованиеСерииЗадач-<?""Номер задачи""> <?"""", ПолноеИмяПользователя> <?"""", ДатаВремя, ""ДЛФ=D""> Начало
// НаименованиеСерииЗадач-<?""Номер задачи""> <?"""", ПолноеИмяПользователя> <?"""", ДатаВремя, ""ДЛФ=D""> Окончание"}
},
{3,
{"ОбщиеОбъекты",1,0,"",""},
{1,
{"Web-сервисы",1,0,"",""},
{0,
{"Вызвов с передачей ОбъектаXDTO",0,0,"","
Определения = Новый WSОпределения(АдресВебСервиса + ""?wsdl"");

// Тип берем именно через Определения, это нужно просто знать
СкладТип = Определения.ФабрикаXDTO.Тип(""http://www.1c-uc3.ru"", ""Склад"");
Склад = ФабрикаXDTO.Создать(СкладТип);

Прокси = Новый WSПрокси(Определения, ""http://www.1c-uc3.ru"", ""Тест"", ""ТестSoap"");

Склад.Наименование = ""ТестовыйСклад"";
Склад.МОЛ = ""Ермаков Виктор Викторович"";
Склад.ТипСклада = ""Розничный"";

// Вызов функции Web-сервиса
СкладСоздан = Прокси.СоздатьСклад(Склад);"}
}
},
{4,
{"JSON",1,0,"",""},
{0,
{"ЗаписьЧтение_РучнаяСериализация",0,0,"","
#Область Запись

Процедура ЗаписьJsonСРучнойСериализацией()
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON;
	
	// Указываем как будет записана дата.
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.JavaScript;
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	
	Запись = Новый ЗаписьJSON;
	Путь = Константы.ПутьДоФайлов.Получить()+""\"";
	Запись.ОткрытьФайл(Путь + ""js.json"");
	
	Стр = Новый Структура(
		""Код, Наименование, ПолноеНаименование"",
		Контрагент.Код, Контрагент.Наименование, Контрагент.ПолноеНаименование());
	
	Массив = Новый Массив;
	Для Каждого ТекСтрока Из Контрагент.Контакты Цикл
		
		СтрКЛ = Новый Структура(""КЛ, Телефон"", """", """");
		СтрКЛ.Кл = ТекСтрока.КонтактноеЛицо;
		СтрКЛ.Телефон = ТекСтрока.Телефон;
		
		Массив.Добавить(СтрКЛ);
		
	КонецЦикла;
	
	Стр.Вставить(""Массив"",Массив);
	Стр.Вставить(""ДатаСведений"",ТекущаяДата());
	Стр.Вставить(""UUID"",Контрагент.УникальныйИдентификатор());
	
	ЗаписатьJSON(Запись, Стр, НастройкиСериализации, ""СериализацияЗначений"", ЭтаФорма);
	Запись.Закрыть();
	
КонецПроцедуры

// Сериализует реквизиты не допустимых типов.
Функция СериализацияЗначений(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Если ТипЗнч(Значение) = Тип(""УникальныйИдентификатор"") Тогда
		Возврат Строка(Значение);
	КонецЕсли;
	
	// Не делаем сериализуцию для других типов.
	Отказ = Истина;
	
КонецФункции

#КонецОбласти

#Область Чтение

Процедура ЧтениеJsonСРучнойСериализацией()
	
	Чтение = Новый ЧтениеJSON;
	Путь = Константы.ПутьДоФайлов.Получить()+""\"";
	Чтение.ОткрытьФайл(Путь+""js.json"");
	
	// Указываем в массиве только те реквизиты, которые нужно преобразовать, для исключения лишних вызовов.
	Реквизиты = Новый Массив;
	Реквизиты.Добавить(""ДатаСведений"");
	Реквизиты.Добавить(""UUID"");
	
	Данные = ПрочитатьJSON(Чтение, Ложь,,, ""ФункцияВосстановленияЧтения"", ЭтаФорма,, Реквизиты);
	Чтение.Закрыть();
	
КонецПроцедуры

// Обратная сериализация типов при чтении.
Функция ФункцияВосстановленияЧтения(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если Свойство = ""ДатаСведений"" Тогда
		Возврат ПрочитатьДатуJSON(Формат(Значение, ""ЧГ=""), ФорматДатыJSON.JavaScript);
	КонецЕсли;
	
	Если Свойство = ""UUID"" Тогда
		Возврат Новый УникальныйИдентификатор(Значение);
	КонецЕсли;
	
КонецФункции

#КонецОбласти"}
},
{0,
{"ЗаписьЧтение_АвтоСериализация",0,0,"","#Область Запись

Процедура ЗаписьJsonСАвтоСериализацией()
	
	Запись = Новый ЗаписьJSON;
	Путь= Константы.ПутьДоФайлов.Получить() + ""\"";
	Запись.ОткрытьФайл(Путь + ""js.json"");
	
	// Используется если тип мы не хотим явно задавать в выгрузке, и он будет определен при загрузке.
	//СериализаторXDTO.ЗаписатьJSON(Запись, Контрагент.ПолучитьОбъект());
	
	СериализаторXDTO.ЗаписатьJSON(Запись, Контрагент.ПолучитьОбъект(), НазначениеТипаXML.Явное);
	
	Запись.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область Загрузка

Процедура ЧтениеJsonСАвтоСериализацией()
	
	Чтение = Новый ЧтениеJSON;
	Путь= Константы.ПутьДоФайлов.Получить()+ ""\"";
	Чтение.ОткрытьФайл(Путь + ""js.json"");
	
	// Используется если при выгрузке не был указан явно тип объекта выгружаемого.
	//Данные = СериализаторXDTO.ПрочитатьJSON(Чтение, Тип(""СправочникОбъект.Контрагенты""));
	
	Данные = СериализаторXDTO.ПрочитатьJSON(Чтение);
	
	Чтение.Закрыть();
	
КонецПроцедуры

#КонецОбласти"}
},
{0,
{"ПотоковаяЗаписьЧтение",0,0,"","
Процедура ПотоковаяЗапись()
	
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, "" "", Истина);
	Запись = Новый ЗаписьJSON;
	
	Путь= Константы.ПутьДоФайлов.Получить() + ""\"";
	Запись.ОткрытьФайл(Путь + ""js.json"",,, ПараметрыJSON);
	
	Запись.ЗаписатьНачалоОбъекта();
	Запись.ЗаписатьИмяСвойства(""СвойствоОбъекта1"");
	Запись.ЗаписатьЗначение(""Значение1"");
	Запись.ЗаписатьИмяСвойства(""СвойствоОбъекта2"");
	Запись.ЗаписатьНачалоОбъекта();
	Запись.ЗаписатьИмяСвойства(""СвойствоВложенногоОбъекта1"");
	Запись.ЗаписатьЗначение(""Зн СВО1"");
	Запись.ЗаписатьИмяСвойства(""СвойствоВложенногоОбъекта2"");
	Запись.ЗаписатьЗначение(""Зн СВО2"");
	Запись.ЗаписатьКонецОбъекта();
	Запись.ЗаписатьИмяСвойства(""Массив"");
	Запись.ЗаписатьНачалоМассива();
	Запись.ЗаписатьЗначение(3.141592654, Истина);
	Запись.ЗаписатьЗначение(2.718281828, Ложь);
	Запись.ЗаписатьКонецМассива();
	Запись.ЗаписатьКонецОбъекта();
	Запись.Закрыть();
	
КонецПроцедуры

Процедура ПотоковоеЧтение()
	
	Чтение = Новый ЧтениеJSON;
	
	Путь= Константы.ПутьДоФайлов.Получить() + ""\"";
	Чтение.ОткрытьФайл(Путь + ""js.json"");
	
	Пока Чтение.Прочитать() Цикл
		
		Сообщить(""Тип текущего элемента "" + Чтение.ТипТекущегоЗначения);
		Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
			Сообщить(""Имя = "" + Чтение.ТекущееЗначение);
		КонецЕсли;
		
		Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Булево Или
			Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Строка Или 
			Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Число Или 
			Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Комментарий Тогда
			Сообщить(""Значение = "" + Чтение.ТекущееЗначение);
		КонецЕсли;
		
	КонецЦикла;
	
	Чтение.Закрыть();
	
КонецПроцедуры


"}
},
{0,
{"СовмещениеТехник",0,0,"","Процедура СовмещениеТехник()
	
	Перем КогдаСформировано, СписокЗаказов;
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON;
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	НастройкиСериализации.ФорматСериализацииДат = ФорматДатыJSON.ISO;
	НастройкиСериализации.СериализовыватьМассивыКакОбъекты = Истина;
	
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, "" "", Истина);
	
	Запись = Новый ЗаписьJSON;
	Запись.ПроверятьСтруктуру = Истина;
	Запись.ОткрытьФайл(""c:\temp\combinedWrite.json"", , , ПараметрыJSON);
	
	Запись.ЗаписатьНачалоОбъекта();
	Запись.ЗаписатьИмяСвойства(""ДатаФормирования"");
	Запись.ЗаписатьЗначение(ЗаписатьДатуJSON(КогдаСформировано, ФорматДатыJSON.ISO, ВариантЗаписиДатыJSON.УниверсальнаяДата));
	Запись.ЗаписатьИмяСвойства(""Заказы"");
	
	Заказы = Новый Массив;
	Для Каждого Заказ Из СписокЗаказов Цикл
		
		ОписаниеЗаказа = Новый Структура(""Ссылка, Номер, Дата, Контрагент"");
		ОписаниеЗаказа.Ссылка = Строка(Заказ.Ссылка);
		ОписаниеЗаказа.Номер = Заказ.Номер;
		ОписаниеЗаказа.Дата = Заказ.Дата;
		ОписаниеЗаказа.Контрагент = Строка(Заказ.Контрагент);
		Заказы.Добавить(ОписаниеЗаказа);
		
	КонецЦикла;
	
	ЗаписатьJSON(Запись, Заказы, НастройкиСериализации);
	Запись.ЗаписатьКонецОбъекта();
	Запись.Закрыть();
	
КонецПроцедуры"}
}
},
{4,
{"HTTP-сервисы",1,0,"",""},
{0,
{"ЗапросGET",0,1,"HTTPСервисЗапрос","
СерверИсточник = ""localhost"";
Адрес = ""/Start/hs/root/nomen/"" + Артикул;
Чтение = Новый ЧтениеJSON;

HTTPЗапрос = Новый HTTPЗапрос(Адрес);
НТТР = Новый HTTPСоединение(СерверИсточник);

#Область ЧтениеВФайл

ИмяВходящегоФайла = ПолучитьИмяВременногоФайла(""json"");

НТТРОтвет = НТТР.Получить(HTTPЗапрос, ИмяВходящегоФайла);
Если НТТРОтвет.КодСостояния = 200 Тогда
	
	Чтение.ОткрытьФайл(ИмяВходящегоФайла);
	// Получаем данные как массив структур
	Данные = ПрочитатьJSON(Чтение, Истина);
	
КонецЕсли;

#КонецОбласти

#Область ЧтениеВДанные

НТТРОтвет = НТТР.Получить(HTTPЗапрос);

Если НТТРОтвет.КодСостояния = 200 Тогда
	
	Чтение.УстановитьСтроку(НТТРОтвет.ПолучитьТелоКакСтроку());
	Данные = ПрочитатьJSON(Чтение, Истина);
	
КонецЕсли;

#КонецОбласти"}
},
{0,
{"ОтветGET",0,1,"HTTPСервисОтвет","Функция ОтветGET(Запрос)
	
	Артикул = Запрос.ПараметрыURL[""Артикул""];
	Ссылка = Справочники.Номенклатура.НайтиПоРеквизиту(""Артикул"", Артикул);
	
	// Если ссылка не найдена ответ 404 (не найдено).
	Если Ссылка.Пустая() Тогда
		Ответ = Новый HTTPСервисОтвет(404);
		Возврат Ответ;
	КонецЕсли;
	
	// При необходимости заполняем параметры записи. 
	// Если параметры не меняются, то и создавть их не нужно, т.к. по умолчанию они такие же.
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON;
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	
	// Используем соответствие, т.к. ключи могут быть сложные.
	Реквизиты = Новый Соответствие;
	Реквизиты.Вставить(""Код"", Ссылка.Код);
	Реквизиты.Вставить(""Наименование"", Ссылка.Наименование);
	Реквизиты.Вставить(""Услуга"", Ссылка.Услуга);
	
	ЗаписатьJSON(Запись, Реквизиты);
	Данные = Запись.Закрыть();
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить(""Content-type"", ""application/json; charset=utf-8"");
	Ответ.УстановитьТелоИзСтроки(Данные);
	
	Возврат Ответ;
	
КонецФункции"}
},
{0,
{"ЗапросPOST",0,0,"","// Вариант когда для ответа нам, нужно послать параметре в json формате строкой.

СерверИсточник = ""localhost"";
Адрес = ""/Start/hs/root/nomen/"" + Артикул;

// При необходимости заполняем параметры записи.
ПараметрыЗаписи = Новый ПараметрыЗаписиJSON;

Запись = Новый ЗаписьJSON;
Запись.УстановитьСтроку(ПараметрыЗаписи);

Стр = Новый Структура(""Наименование, Услуга"");
Стр.Наименование = ""Наименование"";
Стр.Услуга = Истина;

ЗаписатьJSON(Запись, Стр);
Данные = Запись.Закрыть();

HTTPЗапрос = Новый HTTPЗапрос(Адрес);
HTTPЗапрос.УстановитьТелоИзСтроки(Данные);

HTTP = Новый HTTPСоединение(СерверИсточник);
HTTPОтвет = HTTP.ОтправитьДляОбработки(HTTPЗапрос);

Если HTTPОтвет.КодСостояния = 201 Тогда
	ДанныеСтрокой = HTTPОтвет.ПолучитьТелоКакСтроку(""UTF-8"");
	Сообщить(ДанныеСтрокой);
КонецЕсли;"}
},
{0,
{"ОтветPOST",0,1,"HTTPСервисОтветPOST","Функция ОтветPOST(Запрос)
	
	// Отвечаем на запрос с присланными параметрами json в строке запроса.
	
	Артикул = Запрос.ПараметрыURL[""Артикул""];
	Данные = Запрос.ПолучитьТелоКакСтроку(""UTF-8"");
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Данные);
	Данные = ПрочитатьJSON(Чтение, Ложь);
	
	ОбъектНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
	ОбъектНоменклатура.Наименование = Данные.Наименование;
	ОбъектНоменклатура.Услуга = Данные.Услуга;
	ОбъектНоменклатура.Артикул = Данные.Артикул;
	ОбъектНоменклатура.УстановитьНовыйКод();
	ОбъектНоменклатура.Записать();
	
	Ответ = Новый HTTPСервисОтвет(201);
	Ответ.УстановитьТелоИзСтроки(Строка(ОбъектНоменклатура.Ссылка.УникальныйИдентификатор()));
	Возврат Ответ;
	
КонецФункции"}
}
}
},
{1,
{"Глобальный контекст",1,0,"",""},
{1,
{"Процедуры и функции работы с информационной базой",1,0,"",""},
{0,
{"НачатьТранзакцию",0,0,"Тр","НачатьТранзакцию();

Попытка
	
	ЗафиксироватьТранзакцию();
	
Исключение
	
	ОтменитьТранзакцию();
	
	//ЗаписьЖурналаРегистрации(
		//ТипОперации, 
		//УровеньЖурналаРегистрации.Ошибка,, 
		//Текст, 
		//ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	ВызватьИсключение;
	
КонецПопытки;
"}
}
}
}
}
}